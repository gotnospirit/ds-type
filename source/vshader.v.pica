; Example PICA200 vertex shader

; Uniforms
.fvec projection[4]

; Constants
.constf myconst(0.0, 1.0, -1.0, 0.1)
.constf myconst2(0.3, 0.0, 0.0, 0.0)
.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones

; Outputs
.out outpos position
.out outtc0 texcoord0
.out outclr color

.proc main
    ; Force the w component of v0 to be 1.0
    mov r0.xyz, v0
    mov r0.w,   ones

    ; outpos = projectionMatrix * v0
    dp4 outpos.x, projection[0], r0
    dp4 outpos.y, projection[1], r0
    dp4 outpos.z, projection[2], r0
    dp4 outpos.w, projection[3], r0

    ; Fill outtc0 only if v1.w == 0
    mov r1.x, v1.w
    mov r2.x, zeros

    cmp r1, eq, eq, r2

    ifc cmp.x
        ; Set texcoords
        mov outtc0.xy, v1
        mov outtc0.zw, zeros

        mov outclr, zeros
    .else
        ; Set vertex color
        mov outclr, v1

        mov outtc0, zeros
    .end

	end
.end
